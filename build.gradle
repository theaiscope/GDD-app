// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.kotlin_coroutines_version = '1.3.7'
    ext.dagger_version = '2.28'
    ext.mockito_version = '2.28.2'
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'io.fabric.tools:gradle:1.31.2' // Crashlytics plugin
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "io.gitlab.arturbosch.detekt" version "1.9.1"
    id "scabbard.gradle" version "0.3.0"
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://sdk.smartlook.com/android/release" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
