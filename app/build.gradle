apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'io.fabric'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "scabbard.gradle"

buildProperties {
    secrets {
        using rootProject.file('properties/secrets.properties')
    }
    env {
        using System.getenv()
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "net.aiscope.gdd_app"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 20
        versionName "2.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigString 'SMARTLOOK_API_KEY', buildProperties.secrets['SMARTLOOK_API_KEY'].or(buildProperties.env['SMARTLOOK_API_KEY']).or("")
    }

    buildFeatures {
        viewBinding = true
    }
    androidExtensions {
        features = ["parcelize"]
    }
    signingConfigs {
        release {
            storeFile file("../gdd.jks")
            keyAlias 'gdd'
            storePassword buildProperties.secrets['STORE_PW'].or(buildProperties.env['STORE_PW']).or("").string
            keyPassword buildProperties.secrets['KEY_PW'].or(buildProperties.env['KEY_PW']).or("").string
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        animationsDisabled = true
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

scabbard {
    enabled true
}

detekt {
    config = files("$rootProject.projectDir/detekt-config.yml")
    buildUponDefaultConfig = true
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.karumi:dexter:6.2.1'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation 'androidx.work:work-runtime-ktx:2.4.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.activity:activity-ktx:1.2.0-alpha07'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha07'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.1'
    implementation 'com.google.firebase:firebase-common-ktx:19.3.0'
    implementation 'com.google.firebase:firebase-storage-ktx:19.1.1'
    implementation 'com.google.firebase:firebase-firestore-ktx:21.5.0'
    implementation('com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava') {
        because "https://github.com/firebase/firebase-android-sdk/issues/1320"
    }
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlin_coroutines_version"

    // Smartlook – screen recording
    implementation 'com.smartlook.recording:app:1.5.2-native'

    // Timber – logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'

    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.5'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.5'

//    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.3'
//    androidTestImplementation "org.mockito:mockito-inline:$mockito_version"
//    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation('com.nhaarman:mockito-kotlin:1.6.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.0"
    androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'

    // Test
    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"


    // Capture - FotoApparat
    implementation 'io.fotoapparat:fotoapparat:2.7.0'


    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    compileOnly 'javax.annotation:jsr250-api:1.0'
}

apply plugin: 'com.google.gms.google-services'
